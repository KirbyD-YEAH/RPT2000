      ******************************************************************
      * Title..........: RPT2000 - Future Value Calculator
      * Programmer.....: Ben Stearns and Kirby Dunker
      * Date...........: 2-24-26
      * GitHub URL.....: hhttps://github.com/KirbyD-YEAH/RPT2000.git
      * Program Desc...: Generates a sales report for customers based on
      *                  YTD and percent change from last year
      * File Desc......: Define the sole source code for application
      ******************************************************************     
       IDENTIFICATION DIVISION.

       PROGRAM-ID. RPT2000.

       ENVIRONMENT DIVISION.

       INPUT-OUTPUT SECTION.

       FILE-CONTROL.
           SELECT CUSTMAST ASSIGN TO CUSTMAST.
           SELECT SRPT2000 ASSIGN TO SRPT2000.

       DATA DIVISION.

       FILE SECTION.
      
       FD  CUSTMAST
           RECORDING MODE IS F
           LABEL RECORDS ARE OMITTED
           RECORD CONTAINS 130 CHARACTERS
           BLOCK CONTAINS 130 CHARACTERS.

      *****************************************************************
      * The customer master record contains the following fields
      *****************************************************************
       01  CUSTOMER-MASTER-RECORD.
           05  CM-BRANCH-NUMBER        PIC 9(2).
           05  CM-SALESREP-NUMBER      PIC 9(2).
           05  CM-CUSTOMER-NUMBER      PIC 9(5).
           05  CM-CUSTOMER-NAME        PIC X(20).
           05  CM-SALES-THIS-YTD       PIC S9(5)V9(2).
           05  CM-SALES-LAST-YTD       PIC S9(5)V9(2).
           05  FILLER                  PIC X(87).

       FD  SRPT2000
           RECORDING MODE IS F
           LABEL RECORDS ARE OMITTED
           RECORD CONTAINS 130 CHARACTERS
           BLOCK CONTAINS 130 CHARACTERS.

      *****************************************************************
      * Define the print area for the report as a fixed 130 char length
      *****************************************************************     
       01  PRINT-AREA      PIC X(130).

       WORKING-STORAGE SECTION.
      
      *****************************************************************
      * Variable and field definitions for the report
      *****************************************************************

      * Determines when the end of the customer master file is reached
       01  SWITCHES.
           05  CUSTMAST-EOF-SWITCH     PIC X    VALUE "N".
      * Controls spacing on the report and when to print heading lines
       01  PRINT-FIELDS.
           05  PAGE-COUNT      PIC S9(3)   VALUE ZERO.
           05  LINES-ON-PAGE   PIC S9(3)   VALUE +55.
           05  LINE-COUNT      PIC S9(3)   VALUE +99.
           05  SPACE-CONTROL   PIC S9.
      * Totals for the report
       01  TOTAL-FIELDS.
           05  GRAND-TOTAL-THIS-YTD   PIC S9(7)V99   VALUE ZERO.
           05  GRAND-TOTAL-LAST-YTD   PIC S9(7)V99   VALUE ZERO.
           05  GRAND-TOTAL-CHANGE     PIC S9(7)V99   VALUE ZERO.
      * Current date and time fields
       01  CURRENT-DATE-AND-TIME.
           05  CD-YEAR         PIC 9999.
           05  CD-MONTH        PIC 99.
           05  CD-DAY          PIC 99.
           05  CD-HOURS        PIC 99.
           05  CD-MINUTES      PIC 99.
           05  FILLER          PIC X(9).
      * Calculated fields for YTD change amount and percent change
       01  CALCULATED-FIELDS.
           05  CHANGE-AMOUNT   PIC S9(5)V99         VALUE ZERO.
           05  CHANGE-PERCENT  PIC S9(3)V9          VALUE ZERO.
      
      *****************************************************************
      * Define all lines printed on the report
      *****************************************************************
       01  HEADING-LINE-1.
           05  FILLER          PIC X(7)    VALUE "DATE:  ".
           05  HL1-MONTH       PIC 9(2).
           05  FILLER          PIC X(1)    VALUE "/".
           05  HL1-DAY         PIC 9(2).
           05  FILLER          PIC X(1)    VALUE "/".
           05  HL1-YEAR        PIC 9(4).
           05  FILLER          PIC X(11)   VALUE SPACE.
           05  FILLER          PIC X(20)   VALUE "YEAR-TO-DATE SALES R".
           05  FILLER          PIC X(20)   VALUE "EPORT               ".
           05  FILLER          PIC X(8)    VALUE "  PAGE: ".
           05  Hl1-PAGE-NUMBER PIC ZZZ9.
           05  FILLER          PIC X(49)   VALUE SPACE.

       01  HEADING-LINE-2.
           05  FILLER          PIC X(7)    VALUE "TIME:  ".
           05  HL2-HOURS       PIC 9(2).
           05  FILLER          PIC X(1)    VALUE ":".
           05  HL2-MINUTES     PIC 9(2).
           05  FILLER          PIC X(58)   VALUE SPACE.
           05  FILLER          PIC X(10)   VALUE "RPT2000".
           05  FILLER          PIC X(49)   VALUE SPACE.

       01  HEADING-LINE-3.
           05  FILLER      PIC X(130)  VALUE SPACE.

       01  HEADING-LINE-4.
           05  FILLER      PIC X(20)   VALUE "BRANCH SALES CUST   ".
           05  FILLER      PIC X(20)   VALUE SPACE.
           05  FILLER      PIC X(20)   VALUE "     SALES         S".
           05  FILLER      PIC X(20)   VALUE "ALES          CHANGE".
           05  FILLER      PIC X(20)   VALUE "     CHANGE         ".
           05  FILLER      PIC X(29)   VALUE SPACE.

       01  HEADING-LINE-5.
           05  FILLER              PIC X(1)     VALUE SPACE.
           05  FILLER              PIC X(3)     VALUE "NUM".
           05  FILLER              PIC X(4)     VALUE SPACE.
           05  FILLER              PIC X(3)     VALUE "REP".
           05  FILLER              PIC X(1)     VALUE SPACE.
           05  FILLER              PIC X(5)     VALUE "NUM  ".
           05  FILLER              PIC X(2)     VALUE SPACE.
           05  FILLER            PIC X(20) VALUE "CUSTOMER NAME       ".
           05  FILLER              PIC X(3)     VALUE SPACE.
           05  FILLER              PIC X(10)    VALUE " THIS YD ".
           05  FILLER              PIC X(4)     VALUE SPACE.
           05  FILLER              PIC X(12)    VALUE " LAST YTD   ".
           05  FILLER              PIC X(4)     VALUE SPACE.
           05  FILLER              PIC X(10)    VALUE "  AMOUNT  ".
           05  FILLER              PIC X(3)     VALUE SPACE.
           05  FILLER              PIC X(7)     VALUE "PERCENT".
           05  FILLER              PIC X(37)    VALUE SPACE.

       01  HEADING-LINE-6.
           05  FILLER      PIC X(7)             VALUE '------ '.
           05  FILLER      PIC X(6)             VALUE "----- ".
           05  FILLER      PIC X(6)             VALUE "----- ".
           05  FILLER      PIC X(20)            VALUE ALL "-".
           05  FILLER      PIC X(3)             VALUE SPACE.
           05  FILLER      PIC X(14)            VALUE "----------    ".
           05  FILLER      PIC X(17)          VALUE "----------       ".
           05  FILLER      PIC X(12)            VALUE "----------  ".
           05  FILLER      PIC X(7)             VALUE ALL "-".

       01  CUSTOMER-LINE.
           05  FILLER              PIC X(2)     VALUE SPACE.
           05  CL-BRANCH-NUMBER    PIC 9(2).
           05  FILLER              PIC X(4)     VALUE SPACE.
           05  CL-SALESREP-NUMBER  PIC 9(2).
           05  FILLER              PIC X(3)     VALUE SPACE.
           05  CL-CUSTOMER-NUMBER  PIC 9(5).
           05  FILLER              PIC X(2)     VALUE SPACE.
           05  CL-CUSTOMER-NAME    PIC X(20).
           05  FILLER              PIC X(3)     VALUE SPACE.
           05  CL-SALES-THIS-YTD   PIC ZZ,ZZ9.99-.
           05  FILLER              PIC X(4)     VALUE SPACE.
           05  CL-SALES-LAST-YTD   PIC ZZ,ZZ9.99-.
           05  FILLER              PIC X(4)     VALUE SPACE.
           05  CL-CHANGE-AMOUNT    PIC ZZ,ZZ9.99-.
           05  FILLER              PIC X(3)     VALUE SPACE.
           05  CL-CHANGE-PERCENT   PIC ZZ9.9-.
           05  FILLER              PIC X(55)    VALUE SPACE.

       01 HEADING-LINE-7.
           05  FILLER      PIC X(40)            VALUE SPACE.
           05  FILLER      PIC X(13)            VALUE ALL "=".
           05  FILLER      PIC X(1)             VALUE SPACE.
           05  FILLER      PIC X(13)            VALUE ALL "=".
           05  FILLER      PIC X(1)             VALUE SPACE.
           05  FILLER      PIC X(13)            VALUE ALL "=".
           05  FILLER      PIC X(3)             VALUE SPACE.
           05  FILLER      PIC X(6)             VALUE ALL "=".
           05  FILLER      PIC X(39)            VALUE SPACE.

       01  GRAND-TOTAL-LINE.
           05  FILLER              PIC X(40)    VALUE SPACE.
           05  GTL-SALES-THIS-YTD  PIC Z,ZZZ,ZZ9.99-.
           05  FILLER              PIC X(1)     VALUE SPACE.
           05  GTL-SALES-LAST-YTD  PIC Z,ZZZ,ZZ9.99-.
           05  FILLER              PIC X(1)     VALUE SPACE.
           05  GTL-CHANGE-AMOUNT   PIC Z,ZZZ,ZZ9.99-.
           05  FILLER              PIC X(3)     VALUE SPACE.
           05  GTL-CHANGE-PERCENT  PIC ZZ9.9-.
           05  FILLER              PIC X(42)    VALUE SPACE.

       PROCEDURE DIVISION.
      
      *****************************************************************
      * Main processing logic for app
      *****************************************************************
       000-PREPARE-SALES-REPORT.
      * Open the customer master file and the report output file
      * Loop through the customer master file until the end is reached    
           OPEN INPUT  CUSTMAST
                OUTPUT SRPT2000.
           PERFORM 100-FORMAT-REPORT-HEADING.
           PERFORM 200-PREPARE-SALES-LINES
               UNTIL CUSTMAST-EOF-SWITCH = "Y".
           PERFORM 300-PRINT-GRAND-TOTALS.
           CLOSE CUSTMAST
                 SRPT2000.
           STOP RUN.

      *****************************************************************
      * Get current data and time for heading
      *****************************************************************
       100-FORMAT-REPORT-HEADING.

           MOVE FUNCTION CURRENT-DATE TO CURRENT-DATE-AND-TIME.
           MOVE CD-MONTH   TO HL1-MONTH.
           MOVE CD-DAY     TO HL1-DAY.
           MOVE CD-YEAR    TO HL1-YEAR.
           MOVE CD-HOURS   TO HL2-HOURS.
           MOVE CD-MINUTES TO HL2-MINUTES.
      
      *****************************************************************
      * Prepares each customer line until the end of CUSTMAST reachec
      *****************************************************************
       200-PREPARE-SALES-LINES.

           PERFORM 210-READ-CUSTOMER-RECORD.
           IF CUSTMAST-EOF-SWITCH = "N"
              PERFORM 220-PRINT-CUSTOMER-LINE.

       210-READ-CUSTOMER-RECORD.

           READ CUSTMAST
               AT END
                   MOVE "Y" TO CUSTMAST-EOF-SWITCH.
      
      *****************************************************************
      * Gets the data for each customer line, calculates change amount
      * and percent change, and prints the line. Also controls when to
      * print the heading lines based on the number of lines printed on
      *****************************************************************
       220-PRINT-CUSTOMER-LINE.

           IF LINE-COUNT >= LINES-ON-PAGE
               PERFORM 230-PRINT-HEADING-LINES.
           MOVE CM-BRANCH-NUMBER   TO CL-BRANCH-NUMBER.
           MOVE CM-SALESREP-NUMBER TO CL-SALESREP-NUMBER.
           MOVE CM-CUSTOMER-NUMBER  TO CL-CUSTOMER-NUMBER.
           MOVE CM-CUSTOMER-NAME    TO CL-CUSTOMER-NAME.
           MOVE CM-SALES-THIS-YTD   TO CL-SALES-THIS-YTD.
           MOVE CM-SALES-LAST-YTD   TO CL-SALES-LAST-YTD.
           COMPUTE CHANGE-AMOUNT =
                CM-SALES-THIS-YTD - CM-SALES-LAST-YTD.
           MOVE CHANGE-AMOUNT TO CL-CHANGE-AMOUNT.
           *> default for % change is 999.9 if last YTD is 0
           IF CM-SALES-LAST-YTD = ZERO
                MOVE 999.99 TO CL-CHANGE-PERCENT
           ELSE
                COMPUTE CL-CHANGE-PERCENT ROUNDED =
                    CHANGE-AMOUNT * 100 / CM-SALES-LAST-YTD
                    ON SIZE ERROR
                        MOVE 999.9 TO CL-CHANGE-PERCENT.
           MOVE CUSTOMER-LINE TO PRINT-AREA.
           WRITE PRINT-AREA.
           ADD 1 TO LINE-COUNT.
           ADD CM-SALES-THIS-YTD TO GRAND-TOTAL-THIS-YTD.
           ADD CM-SALES-LAST-YTD TO GRAND-TOTAL-LAST-YTD.
           MOVE 1 TO SPACE-CONTROL.
      
      *****************************************************************
      * Prints the heading lines at the top of the report and when the
      * number of lines printed on the page reaches the limit. Also
      * resets the line count and controls spacing on the report
      *****************************************************************
       230-PRINT-HEADING-LINES.

           ADD 1 TO PAGE-COUNT.
           MOVE PAGE-COUNT     TO HL1-PAGE-NUMBER.
           MOVE HEADING-LINE-1 TO PRINT-AREA.
           WRITE PRINT-AREA.
           MOVE HEADING-LINE-2 TO PRINT-AREA.
           WRITE PRINT-AREA.
           MOVE HEADING-LINE-3 TO PRINT-AREA.
           WRITE PRINT-AREA.
           MOVE HEADING-LINE-4 TO PRINT-AREA.
           WRITE PRINT-AREA.
           MOVE HEADING-LINE-5 TO PRINT-AREA.
           WRITE PRINT-AREA.
           MOVE HEADING-LINE-6 TO PRINT-AREA.
           WRITE PRINT-AREA.
           MOVE ZERO TO LINE-COUNT.
           MOVE 2 TO SPACE-CONTROL.
      
      *****************************************************************
      * Get grand totals, compute change amount and % change
      * and print the grand total line at the end of the report
      *****************************************************************
       300-PRINT-GRAND-TOTALS.
           MOVE GRAND-TOTAL-THIS-YTD TO GTL-SALES-THIS-YTD.
           MOVE GRAND-TOTAL-LAST-YTD TO GTL-SALES-LAST-YTD.
           COMPUTE CHANGE-AMOUNT =
                GRAND-TOTAL-THIS-YTD - GRAND-TOTAL-LAST-YTD.
           MOVE CHANGE-AMOUNT TO GTL-CHANGE-AMOUNT.
           IF GRAND-TOTAL-LAST-YTD = ZERO
              MOVE 999.99 TO GTL-CHANGE-PERCENT
           ELSE
                COMPUTE GTL-CHANGE-PERCENT ROUNDED =
                        CHANGE-AMOUNT * 100 / GRAND-TOTAL-LAST-YTD
                        ON SIZE ERROR
                            MOVE 999.9 TO GTL-CHANGE-PERCENT.
           MOVE HEADING-LINE-7 TO PRINT-AREA.
           WRITE PRINT-AREA.
           MOVE GRAND-TOTAL-LINE     TO PRINT-AREA.
           WRITE PRINT-AREA.

